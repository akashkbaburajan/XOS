alias physicalSP S0;
physicalSP = ([PTBR + 2 * (SP / 512)] * 512) + (SP % 512);

alias sysCallNo S1;
sysCallNo = [physicalSP - 1];

alias PID S4;
PID = (PTBR - 1024) / 8; 

alias currentPCB S3;
currentPCB = PID*32 + READY_LIST;
	
alias counter S5;
alias nextPID S2;

if(sysCallNo == 10 || sysCallNo == 14) then	//exit, signal

	//need to wake uup processes
	
	counter = 0;
	while(counter < 32) do
		if([READY_LIST + counter*32 + 1] == 100 + counter) then
			[READY_LIST + counter*32 + 1] = 1;
		endif;
		counter = counter + 1;
	endwhile;
	
	if(sysCallNo == 14) then
		[physicalSP - 2] = 0;
		ireturn;
	endif;
endif;


if (sysCallNo == 10 || sysCallNo == 13) then
	
	// search for any ready process in ready list
	nextPID = 0;
	while (nextPID < 32) do
		if ([READY_LIST + 32 * nextPID + 1] == 1) then
			break;
		endif;
		nextPID = nextPID + 1;
	endwhile;	
	
endif;

if(sysCallNo == 10) then	//exit

	if(nextPID == 32) then	//halt if no more processes in mem
		halt;
	endif;

	counter = 0;	

	//closing open files
	while (counter < 8) do		
		if ([currentPCB + 15 + counter*2] != -1) then
			//decrement file count in SWOFT
			[FILE_TABLE + [currentPCB + 15 + counter * 2] * 2 + 1] = [FILE_TABLE + [currentPCB + 15 + counter * 2] * 2 + 1] - 1;
			if ([FILE_TABLE + [currentPCB + 15 + counter * 2] * 2 + 1] == 0) then				
			// invalidate the entry in SWOFT
			[FILE_TABLE+[currentPCB+15+counter*2]*2] = -1;
			endif;
			// invalidate the PPOFT by setting fields to -1
			[currentPCB + 15 + counter * 2] = -1;
			[currentPCB + 15 + counter * 2 + 1] = -1;
		endif;
		counter = counter + 1;
	endwhile;
	
	[currentPCB + 1] = 0;		//state = 0

	//invalidating PTBR and mem freed
	counter = 0;
	while (counter < 4) do
		if ([PTBR + counter * 2] == -1) then
			// already invalid
			counter = counter + 1;
			continue;
		endif;
		[MEM_LIST + [PTBR + counter * 2]] = 0;	//mem freed
		[PTBR + counter * 2] = -1;	//invalidate PTBR
		[PTBR + counter * 2 + 1] = "00";
		counter = counter + 1;
	endwhile;	

endif;


if(sysCallNo == 13) then	//wait

	alias waitPID S8;
	waitPID = [physicalSP - 3];
	if([READY_LIST+waitPID*32+1]==0||[READY_LIST+waitPID*32+1]==2) then
		[physicalSP-2] = -1;
		ireturn;
	endif;

	// saving context of current process.
	[currentPCB + 1] = 100 + waitPID;
	[currentPCB + 2] = BP;
	[currentPCB + 3] = SP - 1;
	[currentPCB + 4] = [physicalSP];
	[currentPCB + 5] = PTBR;
	[currentPCB + 6] = PTLR;
	[currentPCB + 7] = R0;
	[currentPCB + 8] = R1;
	[currentPCB + 9] = R2;
	[currentPCB + 10] = R3;
	[currentPCB + 11] = R4;
	[currentPCB + 12] = R5;
	[currentPCB + 13] = R6;
	[currentPCB + 14] = R7;

endif;
	
if (sysCallNo == 10 || sysCallNo == 13) then

	//set up next process to run...like timer

	alias newPCB S6;
	newPCB = READY_LIST + nextPID * 32;
	
	BP = [newPCB + 2];
	SP = [newPCB + 3];	
	PTBR = [newPCB + 5];
	PTLR = [newPCB + 6];
	R0 = [newPCB + 7];
	R1 = [newPCB + 8];
	R2 = [newPCB + 9];
	R3 = [newPCB + 10];
	R4 = [newPCB + 11];
	R5 = [newPCB + 12];
	R6 = [newPCB + 13];
	R7 = [newPCB + 14];
	
	SP = SP + 1;
	alias newPhysicalSP S7;
	newPhysicalSP = ([PTBR + 2 * (SP / 512)] * 512) + (SP % 512);
	[newPhysicalSP] = [newPCB + 4];		//IP
	
	[newPCB + 1] = 2;	//run

	ireturn;
endif;














